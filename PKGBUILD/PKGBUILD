# Maintainer: Aleix Bon√© <abone9999@gmail.com>
pkgname=python-jutge_cli-git 
pkgver=r86.222f8e6
pkgrel=1
pkgdesc="Command line utilities for jutge.org online jutge"
arch=('any')
url="https://github.com/Leixb/jutgeutils"
license=('GPL3')
depends=('python' 'python-beautifulsoup4' 'python-requests' 'python-argparse' 'python-pypandoc' 'python-yaml')
makedepends=('git') 
provides=("${pkgname%-git}")
conflicts=("jutgeutils-git" "${pkgname%-git}")
source=("git+https://github.com/Leixb/jutge_cli")
md5sums=('SKIP')

pkgver() {
    name="${pkgname#python-}"
    cd "$srcdir/${name%-git}"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    name="${pkgname#python-}"
    cd "$srcdir/${name%-git}"

    python setup.py install --root="${pkgdir}" --optimize=1

    # Add completion for zsh
    completion_folder="${pkgdir}/usr/share/zsh/functions/Completion/Unix"
    mkdir -p "${completion_folder}"

    cp zsh_completion/_jutge "${completion_folder}/_jutge"
}
